prop World.FetchedLeaderboard

pub page fn owner.HomePage {
    Toolbar {
        FlatButton(AboutPage) {
            Icon("fa-solid fa-info-circle")
            " About"
        }
        FlatButton(ShareIntent) {
            Icon("fa-solid fa-share")
            " Share"
        }
    }

    ToolbarRight {
        if IsUserLoggedIn {
            FlatButton(ManageAccountIntent, backgroundColor=#fff1) { %(PlayerName) }
        } else {
            FlatButton(LoginIntent, backgroundColor=Color:Primary) { "Login" }
        }
    }
    
    ContentScreen(expand=false) {
        VStack(gap=0) {
            VStack(expand=true, vAlign=VAlign:Bottom, backgroundColor=#5045, vPadding=2) {
                Blank(height=3)
                
                Standout(margin=0, animate=Animate:FlyFromTop) { Icon("fa-solid fa-bolt") }
                Standout(margin=0, animate=Animate:Throb) {
                    FlyInWords(ProjectName)
                }
                P(fontSize=1.2) {
                    "You're "
                    Span(bold=true, color=Color:Energy) { "autistic" }
                    ". Can you get through a "
                    Span(bold=true, color=Color:Energy) { "job interview" }
                    "?"
                }
            }
            
            VStack(align=Align:Center, vAlign=VAlign:Middle, backgroundColor=#5041, vPadding=2) {
                VStack(gap=0.3) {
                    P(fontSize=0.8) { "Choose Your Name" }
                    PlayerNameEditor(width=30)
                }
        
                RaisedButton(Main, backgroundColor=Color:Primary, width=30, fontSize=1.5) { "Play" }

            }
        }
    }

    Content {
        Blank(height=2)

        VStack(align=Align:Center) {
            H2 { "Leaderboard" }

            if !IsUserLoggedIn && BestSalary > 0 {
                ShinyPanel(align=Align:Center, vPadding=0.5, backgroundColor=#f60) {
                    P(fontSize=0.8) {
                        Icon("fa-solid fa-triangle-exclamation")
                        " Unregistered high scores get deleted after 90 days. To keep your high score forever, please "
                        Link(LoginIntent) { "login" }
                        "."
                    }
                }
            }

            with FetchedLeaderboard, PlayerName {
                if FetchedLeaderboard {
                    let foundMyself = false
                    let rank = 1
                    for player in FetchedLeaderboard %%{
                        let isMe = player.UserId == owner.UserId
                        if isMe { foundMyself = true }
                        
                        ShinyPanel(backgroundColor = isMe ? #b0f : Color:Energy, vPadding = isMe ? 0.6 : 0.4) {
                            HStack {
                                P(fontSize=0.75, color=#eee, width=1.5, align=Align:Center) {
                                    %(rank)
                                }
                                P(fontSize=isMe ? 1.5 : 1.2, bold=true) {
                                    PlayerNameDisplay(isMe ? owner : player, color=#fff)
                                }
            
                                Blank(expand=true)
            
                                P {
                                    Icon("fa-solid fa-coins")
                                    %(player.BestSalary.Round)
                                }
                            }
                        }
            
                        rank += 1
                    }
            
                    if FetchedLeaderboard.IsEmpty {
                        P { "No one on the leaderboard... yet" }
                    }
                } else {
                    P { "Loading..." }
                }
            }
        }
    }

    once FetchLeaderboard([&BestSalary]) rankings {
        FetchedLeaderboard = rankings
    }
}