field msg.MsgText
field msg.MsgIsFromMe

pub prop chat.ChatIsTyping

field chat.ChatMessages
signal chat.ChatMessagesChanged

pub fn chat.ChatWindow(sender) {
    ChatMessages = []
    ChatReplyControls
    
    OverlayContent {
        Panel(padding=0, width=45, backgroundColor=#fff1, animate=Animate:FlyFromBottom) {
            FlatPanel(backgroundColor=Color:Primary, padding=0) {
                HStack {
                    HStack(padding=1) {
                        P {
                            "Chat with " + sender
                        }
                    }
                }
            }
            VStack(expand=true, padding=1) {
                VStack(expand=true, vAlign=VAlign:Bottom, padding=1, gap=1.5, height=20, overflow=false) {
                    with ChatMessagesChanged {
                        await Paint
                        for use msg in ChatMessages %%{
                            HStack(
                                align = MsgIsFromMe ? Align:Right : Align:Left) {
                                VStack(gap=0.3) {
                                    P(fontSize=0.8, color=#fffa) {
                                        %(MsgIsFromMe ? "You" : sender)
                                    }
                                    Panel(
                                        maxWidth=35,
                                        backgroundColor = MsgIsFromMe ? Color:Primary : #444) {

                                        P(color=#fff) {
                                            %(MsgText)
                                        }
                                    }
                                }
                            }
                        }
                    }
                }

                VStack(minHeight=1.5) {
                    with ChatIsTyping {
                        if ChatIsTyping {
                            P(fontSize=0.9, color=#fffa) {
                                %(sender + " is typing...")
                            }
                        }
                    }
                }
                
                ChatReplyPanel
            }
        }

        ChatReplyHintPanel
    }
}

pub fn chat.ReceiveChat(text) {
    ChatIsTyping = true
    await Tick(0.05s * text.Length)
    ChatIsTyping = false
    PushChat(text)
}

pub fn chat.SendChat(text) {
    PushChat(text=, isMe=true)
}

fn chat.PushChat(text, isMe?) {
    chat.ChatMessages.Push({ MsgIsFromMe = isMe ?? false, MsgText = text })
    while chat.ChatMessages.Length > 10 {
        chat.ChatMessages.Shift
    }
    ChatMessagesChanged
}