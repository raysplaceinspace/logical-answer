field msg.MsgText
field msg.MsgIsFromMe

pub prop chat.ChatIsTyping

field chat.ChatMessages
signal chat.ChatMessagesChanged

pub prop chat.ChatReply
pub signal World.AcceptChatReply
pub signal World.ChangeChatReplyDown
pub signal World.ChangeChatReplyUp

pub prop chat.ChatReplyHint
pub prop chat.ChatReplyEnergyCost = 1
pub symbol ChatReplyHint:AlternativesAvailable
pub symbol ChatReplyHint:KeepHolding
pub symbol ChatReplyHint:CyclingAvailable
pub symbol ChatReplyHint:NotEnoughEnergy

pub fn chat.ChatWindow(sender) {
    ChatMessages = []
    
    OverlayContent {
        Panel(padding=0, width=45, backgroundColor=#fff1, animate=Animate:FlyFromBottom) {
            FlatPanel(backgroundColor=Color:Primary, padding=0) {
                HStack {
                    HStack(padding=1) {
                        P {
                            "Chat with " + sender
                        }
                    }
                }
            }
            VStack(expand=true, padding=1) {
                VStack(expand=true, vAlign=VAlign:Bottom, padding=1, gap=1.5, height=20, overflow=false) {
                    with ChatMessagesChanged {
                        await Paint
                        for use msg in ChatMessages %%{
                            HStack(
                                align = MsgIsFromMe ? Align:Right : Align:Left) {
                                VStack(gap=0.3) {
                                    P(fontSize=0.8, color=#fffa) {
                                        %(MsgIsFromMe ? "You" : sender)
                                    }
                                    Panel(
                                        maxWidth=35,
                                        backgroundColor = MsgIsFromMe ? Color:Primary : #444) {

                                        P(color=#fff) {
                                            %(MsgText)
                                        }
                                    }
                                }
                            }
                        }
                    }
                }

                VStack(minHeight=1.5) {
                    with ChatIsTyping {
                        if ChatIsTyping {
                            P(fontSize=0.9, color=#fffa) {
                                %(sender + " is typing...")
                            }
                        }
                    }
                }
                
                InsetPanel(padding=0) {
                    HStack(hPadding=0.5, vPadding=0.5, vAlign=VAlign:Middle) {
                        with ChatReplyHint {
                            Link(
                                HoldIntent<changeReply>,
                                bold=false, expand=true, padding=0.5, align=Align:Left,
                                color = ChatReplyHint ? undefined : #fff) {
                                P {
                                    with ChatReply {
                                        let revealLength = 1
                                        with Tick(0.02s) {
                                            %(ChatReply.Substring(0, revealLength))
                                            if revealLength < ChatReply.Length {
                                                revealLength += 1
                                            } else {
                                                break
                                            }
                                        }
                                    }
                                }
                            }
                        }

                        with ChatReply {
                            if ChatReply {
                                RaisedButton(PressIntent<reply>, backgroundColor=Color:Primary) { "Send" }
                            } else {
                                RaisedButton(backgroundColor=Color:Primary, opacity=0.5) { "Send" }
                            }
                        }
                    }
                }
            }
        }
        VStack(minHeight=1.5) {
            P(fontSize=0.9, align=Align:Center, color=#fffa) {
                with ChatReplyHint, ChatReplyEnergyCost {
                    if ChatReplyHint == ChatReplyHint:AlternativesAvailable {
                        "You can "
                        Span(bold=true) { "click and hold" }
                        " your reply to think of something else to say ("
                        Span(bold=true, color=Color:Energy) {
                            "-" + ChatReplyEnergyCost + " Energy "
                            Icon("fa-solid fa-bolt")
                        }
                        ")"
                    } else if ChatReplyHint == ChatReplyHint:KeepHolding {
                        Span(bold=true) { "Keep holding" }
                        "... thinking of something else to say"
                    } else if ChatReplyHint == ChatReplyHint:NotEnoughEnergy {
                        Span(bold=true, color=Color:Negative) { "Not enough energy" }
                    } else if ChatReplyHint == ChatReplyHint:CyclingAvailable {
                        Span(bold=true) { "Click" }
                        " your reply to cycle between options"
                    }
                }
            }
        }
    }

    on Pressed<changeReply> {
        ChangeChatReplyDown
    }
    on Unpressed<changeReply> {
        ChangeChatReplyUp
    }
    on Pressed<reply> {
        AcceptChatReply
    }
}

pub fn chat.ReceiveChat(text) {
    ChatIsTyping = true
    await Tick(0.05s * text.Length)
    ChatIsTyping = false
    PushChat(text)
}

pub fn chat.SendChat(text) {
    PushChat(text=, isMe=true)
    ChatReply = null
    ChatReplyHint = null
}

pub fn chat.PushChat(text, isMe?) {
    chat.ChatMessages.Push({ MsgIsFromMe = isMe ?? false, MsgText = text })
    while chat.ChatMessages.Length > 10 {
        chat.ChatMessages.Shift
    }
    ChatMessagesChanged
}