pub accumulator owner.BestSalary

pub page fn owner.LeaderboardPage() {
    Content {
        H1 { "Leaderboard" }
        P {
            "Who got offered the highest salary?"
        }
        if !IsUserLoggedIn && BestSalary > 0 {
            ShinyPanel(align=Align:Center, vPadding=0.5, backgroundColor=#f60) {
                P(fontSize=0.8) {
                    Icon("fa-solid fa-triangle-exclamation")
                    " Unregistered high scores get deleted after 90 days. To keep your score forever, please "
                    Link(LoginIntent) { "login" }
                    "."
                }
            }
        }
        InsetPanel(align=Align:Center, vPadding=1) {
            P {
                "Think you can do better? "
            }
            RaisedButton(Main, backgroundColor=Color:Primary, fontSize=1.5, width=25) { "Play" }
        }
        P(fontSize=0.8, color=#aaa) {
            "This game was made with "
            Link("https://easel.games") { "Easel" }
            " - the beginner-friendly programming language for multiplayer games!"
        }
    }
    
    Content<leaderboard> {
        P { "Loading..." }
    }
    
    let ranking = await FetchLeaderboard([&BestSalary])
    Content<leaderboard> {
        let rank = 1
        for player in ranking %%{
            ShinyPanel(backgroundColor=Color:Energy, vPadding=0.4) {
                HStack {
                    P(fontSize=0.75, color=#eee, width=1.5, align=Align:Center) {
                        %(rank)
                    }
                    P(fontSize=1.2, bold=true) {
                        PlayerNameDisplay(player)
                    }

                    Blank(expand=true)

                    P {
                        %("$" + player.BestSalary.Round)
                    }
                }
            }

            rank += 1
        }

        if ranking.Length == 0 {
            P { "No one on the leaderboard... yet" }
        }
    }
}

pub fn owner.SubmitToLeaderboard(salary) -> isNewHighScore {
    if salary > BestSalary {
        BestSalary(overwrite=salary, showOnLeaderboard=true)
        return true
    } else {
        return false
    }
}